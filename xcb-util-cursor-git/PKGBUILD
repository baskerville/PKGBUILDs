# Maintainer: Bastien Dejean <nihilhill@gmail.com>

_pkgname=util-cursor
pkgname=xcb-${_pkgname}-git
pkgver=30
pkgrel=1
pkgdesc='XCB cursor management library'
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/xcb/${_pkgname}"
license=('custom:MIT')
depends=('libxcb' 'xcb-util-renderutil' 'xcb-util-image')
makedepends=('git' 'xorg-util-macros' 'gperf')
provides=("xcb-${_pkgname}")
conflicts=("xcb-${_pkgname}")
source=("git://anongit.freedesktop.org/xcb/${_pkgname}")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    git rev-list --count HEAD
}

build() {
    cd "$srcdir/$_pkgname"
    git submodule update --init
    ./autogen.sh --prefix=/usr
    make
}

package() {
    cd "$srcdir/$_pkgname"
    make DESTDIR="$pkgdir" install
}
