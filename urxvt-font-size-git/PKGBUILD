# Maintainer: baskerville <baskerville a lavabit o com>

pkgname=urxvt-font-size-git
pkgver=20120614
pkgrel=1
pkgdesc="On-the-fly URxvt font size adjustments"
arch=('any')
url="https://github.com/majutsushi/urxvt-font-size"
license=('MIT')
depends=('rxvt-unicode' 'xorg-xrdb' 'xorg-xlsfonts')
makedepends=('git')
provides=('urxvt-font-size')
conflicts=('urxvt-font-size')
install='urxvt-font-size.install'
_gitroot='git://github.com/majutsushi/urxvt-font-size.git'
_gitname='urxvt-font-size'

build() {
    cd "$srcdir"
    if [[ -d "$_gitname" ]] ; then
        cd "$_gitname" && git pull origin
    else
        git clone "$_gitroot" "$_gitname"
    fi
}

package() {
    cd "$srcdir/$_gitname"
    install -D -m755 font-size "$pkgdir/usr/lib/urxvt/perl/font-size"
    install -D -m644 README.markdown "$pkgdir/usr/share/doc/$pkgname/README.markdown"
}
