# Maintainer: Bastien Dejean <baskerville@lavabit.com>

_pkgname=sxiv
pkgname=${_pkgname}-git
pkgver=1
pkgrel=1
pkgdesc='A simple X image viewer'
arch=('i686' 'x86_64')
license=('GPL2')
url="https://github.com/muennich/${_pkgname}"
depends=('imlib2' 'giflib')
makedepends=('git')
optdepends=('imagemagick: for the example commands in config.def.h'
            'libjpeg-turbo: for the example commands in config.def.h'
            'gimp: for the example commands in config.def.h')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git://github.com/muennich/${_pkgname}.git")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    git describe --always | tr '-' '.'
}

build() {
    cd "$srcdir/$_pkgname"
    if [ -e "$startdir"/config.h ] ; then
        msg "Using custom 'config.h'."
        cp "$startdir"/config.h .
    fi
    make PREFIX=/usr
}

package() {
    cd "$srcdir/$_pkgname"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -D -m644 sxiv.desktop "${pkgdir}/usr/share/applications/sxiv.desktop"
}
