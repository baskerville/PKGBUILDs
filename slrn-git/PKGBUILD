# Maintainer: Bastien Dejean <baskerville at lavabit.com>

pkgname=slrn-git
pkgver=20120413
pkgrel=1
pkgdesc="An open source text-based news client"
arch=('i686' 'x86_64')
url="http://www.slrn.org/"
license=('GPL')
depends=('slang-git' 'uudeview')
makedepends=('git')
provides=('slrn')
conflicts=('slrn')
backup=(etc/slrnrc)

_gitroot='git://git.jedsoft.org/git/slrn.git'
_gitname='slrn'

build() {
    cd "$srcdir"
    if [ -d $_gitname ] ; then
        cd $_gitname && git pull origin
    else
        git clone $_gitroot $_gitname
    fi

    rm -rf "$srcdir/$_gitname-build"
    git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

    cd "$srcdir/$_gitname-build"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --enable-setgid-code \
        --with-slrnpull --with-ssl --with-uu \
        --with-uulib=/usr/lib/uudeview \
        --with-uuinc=/usr/include/uudeview \
        --enable-spoilers
    make
}

package() {
    cd "$srcdir/$_gitname-build"
    make DESTDIR="$pkgdir" install
    install -Dm 755 contrib/cleanscore $pkgdir/usr/bin/cleanscore
    install -Dm 644 doc/slrn.rc $pkgdir/etc/slrnrc
}
