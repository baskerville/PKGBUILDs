# Maintainer: Bastien Dejean <baskerville at lavabit dot com>

pkgname=mutt-bare-hg
pkgver=5543
pkgrel=1
pkgdesc='Mutt with S-Lang and without built-in POP/SMTP/IMAP/SSL'
url='http://www.mutt.org/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gpgme' 'slang-git' 'libidn')
makedepends=('pkgconfig' 'mercurial')
provides=('mutt')
conflicts=('mutt')
backup=('etc/Muttrc')
source=('mutt_bool.patch')
md5sums=('3c8c5e060972e83488867b609686cf2f')
hgroot='http://dev.mutt.org/hg/mutt#HEAD'
hgrepo='mutt'

build() {
    cd "$srcdir"
    if [ -d "$hgrepo" ] ; then
        cd "$hgrepo"
        hg pull -u
    else
        hg clone "$hgroot" "$hgrepo"
        cd "$hgrepo"
        hg pull -u
    fi

    cd "$srcdir"
    rm -rf "${hgrepo}-build" > /dev/null 2>&1 || true
    cp -r "$hgrepo" "${hgrepo}-build"

    cd "${srcdir}/${hgrepo}-build"
    patch -i "${srcdir}/mutt_bool.patch"
    ./prepare \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-gpgme \
        --enable-hcache \
        --with-slang=/usr \
        --with-regex \
        --with-idn
    make
}

package() {
    cd "${srcdir}/${hgrepo}-build"
    make DESTDIR="${pkgdir}" install
    rm "${pkgdir}"/usr/bin/{flea,muttbug}
    rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
    rm "${pkgdir}"/etc/mime.types{,.dist}
    install -Dm644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
