# Maintainer: Bastien Dejean <baskerville at lavabit dot com>

pkgname=mutt-bare-hg
pkgver=5543
pkgrel=1
pkgdesc='mutt without built-in POP/SMTP/IMAP/SSL'
url='http://www.mutt.org/'
license=('GPL')
backup=('etc/Muttrc')
arch=('i686' 'x86_64')
depends=('gpgme' 'ncurses' 'libidn')
makedepends=('pkgconfig' 'mercurial')
provides=('mutt')
conflicts=('mutt')
hgroot='http://dev.mutt.org/hg/mutt#HEAD'
hgrepo='mutt'

build() {
    cd "$srcdir"
    if [ -d "$hgrepo" ] ; then
        cd "$hgrepo"
        hg pull -u
    else
        hg clone "$hgroot" "$hgrepo"
        cd "$hgrepo"
        hg pull -u
    fi

    cd "$srcdir"
    rm -rf "${hgrepo}-build" > /dev/null 2>&1 || true
    cp -r "$hgrepo" "${hgrepo}-build"

    cd "${srcdir}/${hgrepo}-build"
    ./prepare \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-gpgme \
        --enable-hcache \
        --with-curses=/usr \
        --with-regex \
        --with-idn
    make
}

package() {
    cd "${srcdir}/${hgrepo}-build"
    make DESTDIR="${pkgdir}" install
    rm "${pkgdir}"/usr/bin/{flea,muttbug}
    rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
    rm "${pkgdir}"/etc/mime.types{,.dist}
    install -Dm644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
