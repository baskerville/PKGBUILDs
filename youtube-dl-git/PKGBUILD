# Maintainer: Bastien Dejean <baskerville@lavabit.com>

_pkgname=youtube-dl
pkgname=${_pkgname}-git
pkgver=2013.05.10z6zg86fd453
pkgrel=1
pkgdesc='Small command-line program to download videos from YouTube.com and other video sites'
arch=('i686' 'x86_64')
url="http://rg3.github.com/${_pkgname}"
license=('custom')
depends=('python2')
makedepends=('git' 'zip' 'sed' 'haskell-pandoc')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git://github.com/rg3/${_pkgname}.git")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    git describe --always | tr '-' 'z'
}

build() {
    cd "$srcdir/$_pkgname"
    sed -i 's:/usr/bin/env python:/usr/bin/env python2:' Makefile
    sed -i 's/python -m /python2 -m /' Makefile
    make
}

package() {
    cd "$srcdir/$_pkgname"
    mkdir -p "${pkgdir}/usr/bin"
    cp -p youtube-dl "${pkgdir}/usr/bin"
    mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
    grep -i license youtube_dl/__init__.py > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
