# Maintainer: Bastien Dejean <baskerville@lavabit.com>

_pkgname=youtube-dl
pkgname=${_pkgname}-git
pkgver=1706
pkgrel=1
pkgdesc='Small command-line program to download videos from YouTube.com and other video sites'
arch=('i686' 'x86_64')
url="http://rg3.github.com/${_pkgname}"
license=('custom')
depends=('python2')
makedepends=('git' 'zip' 'sed')
optdepends=('haskell-pandoc: to generate the manual')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git://github.com/rg3/${_pkgname}.git")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    git rev-list --count HEAD
}

build() {
    cd "$srcdir/$_pkgname"
    pandoc -v > /dev/null 2>&1 || \
        sed -i 's/\t\(pandoc \)/\t#\1/;s/.*\$(MANDIR).*//' Makefile
    make
}

package() {
    cd "$srcdir/$_pkgname"
    make PREFIX=/usr DESTDIR="${pkgdir}" install
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
