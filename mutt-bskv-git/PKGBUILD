# Maintainer: Bastien Dejean <baskerville at lavabit dot com>

_pkgname=mutt
pkgname=${_pkgname}-bskv-hg
pkgver=5647
pkgrel=1
pkgdesc='Mutt with S-Lang and without built-in POP/SMTP/IMAP/SSL'
url='http://www.mutt.org/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gpgme' 'slang-git' 'libidn')
makedepends=('pkgconfig' 'mercurial')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
backup=('etc/Muttrc')
source=("hg+http://dev.${_pkgname}.org/hg/${_pkgname}#branch=HEAD" 'mutt_bool.patch')
md5sums=('SKIP' '3c8c5e060972e83488867b609686cf2f')

pkgver() {
    cd "$srcdir/$_pkgname"
    hg identify -n
}

prepare() {
    cd "$srcdir/$_pkgname"
    patch -i "${srcdir}/mutt_bool.patch"
}

build() {
    cd "$srcdir/$_pkgname"
    ./prepare \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-gpgme \
        --enable-hcache \
        --with-slang=/usr \
        --with-regex \
        --with-idn
    make
}

package() {
    cd "$srcdir/$_pkgname"
    make DESTDIR="$pkgdir" install
    rm "$pkgdir"/usr/bin/{flea,muttbug}
    rm "$pkgdir"/usr/share/man/man1/{flea,muttbug}.1
    rm "$pkgdir"/etc/mime.types{,.dist}
    install -Dm644 contrib/gpg.rc "$pkgdir"/etc/Muttrc.gpg.dist
}
