# Maintainer: Bastien Dejean <baskerville at lavabit.com>

pkgname=slang-git
pkgver=20120927
pkgrel=1
pkgdesc='S-Lang is a powerful interpreted language'
arch=('i686' 'x86_64')
url='http://www.jedsoft.org/slang/index.html'
license=('GPL2')
depends=('glibc' 'pcre' 'zlib')
optdepends=('oniguruma')
makedepends=('git')
provides=('slang-git')
conflicts=('slang')

_gitroot='git://git.jedsoft.org/git/slang.git'
_gitname='slang'

build() {
    cd "$srcdir"
    if [ -d $_gitname ] ; then
        cd $_gitname && git pull origin
    else
        git clone $_gitroot $_gitname
    fi

    rm -rf "$srcdir/$_gitname-build"
    git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

    cd "$srcdir/$_gitname-build"
    ./configure --prefix=/usr
    make
}

package() {
    cd "$srcdir/$_gitname-build"
    make DESTDIR="$pkgdir" install
}
